plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // TestNG dependency
    testImplementation 'org.testng:testng:6.9.10'

    // Selenium dependency
    implementation 'org.seleniumhq.selenium:selenium-java:4.21.0'
}

buildDir = 'build'

test {
    useTestNG() {
        useDefaultListeners = true // generates the TestNG default report
        suites "src/test/resources/testng.xml" // Make sure the path is correct
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTEST RESULT: ${result.resultType}"
            println "TEST SUMMARY: RAN ${result.testCount} TESTS, " +
                    "${result.successfulTestCount} SUCCEEDED, " +
                    "${result.failedTestCount} FAILED, " +
                    "${result.skippedTestCount} SKIPPED"
        }
    }

    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat = 'full'
        showStandardStreams = true
    }

    // Ensure test results are never marked as up-to-date (so tests always run)
    test.outputs.upToDateWhen { false }
}

application {
    mainClassName = 'demo.App' // Adjust this to your main class name
}

checkstyle {
    toolVersion = '8.45' // Update this version to the latest as needed
    config = rootProject.resources.text.fromFile('__CRIO__/checkstyle.xml')
    sourceSets = [sourceSets.test]
    ignoreFailures = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = false
    }
}

test {
    dependsOn assemble
    mustRunAfter clean
}
